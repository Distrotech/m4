# Configure template for GNU m4.			-*-Autoconf-*-
# Copyright 1991-1994, 2000, 2001  Free Software Foundation, Inc.
#  
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA
# 02111-1307  USA

define([MY_NAME],    [m4])
define([MY_VERSION], [1.4q])

AC_PREREQ(2.52)
ifdef([m4_pattern_allow], [m4_pattern_allow([^m4_cv_])])dnl
ifdef([changeword], [undefine([changeword])])dnl

AC_INIT(MY_NAME, MY_VERSION, [bug-m4@gnu.org])
AC_CONFIG_SRCDIR([src/m4.h])
AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config.h:config-h.in)

AM_INIT_AUTOMAKE(MY_NAME, MY_VERSION)

AC_PROG_CC
AC_ISC_POSIX
AM_PROG_CC_STDC
AC_PROG_CPP
AC_PROG_CC_C_O

ALL_LINGUAS="cs de el fr it ja nl pl ru sv"
AM_GNU_GETTEXT

pkglibexecdir='${libexecdir}'/$PACKAGE
AC_SUBST(pkglibexecdir)

AM_ENABLE_SHARED
AC_LIBTOOL_DLOPEN
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PATH_PROG(PERL,perl)

AM_C_PROTOTYPES
AC_C_CONST
M4_AC_CHECK_DEBUGGING

AC_LIB_LTDL

AC_CHECK_HEADERS(limits.h locale.h memory.h string.h unistd.h errno.h)
AC_HEADER_STDC
AC_TYPE_SIZE_T

LIBM4_DLL_IMPORT=
case "$host" in
*-*-cygwin* | *-*-mingw* | *-*-os2)
  if test "X$enable_shared" = Xyes; then
    LIBM4_DLL_IMPORT=-DLIBM4_DLL_IMPORT
  fi
  ;;
esac
AC_SUBST(LIBM4_DLL_IMPORT)

AC_CHECK_FUNCS(bzero calloc mkstemp mktemp strerror tmpfile)

AC_FUNC_ALLOCA
AC_FUNC_VPRINTF
AC_REPLACE_FUNCS(strtol xmalloc xstrdup)

M4OBJS=
ifdef([m4_pattern_allow], [m4_pattern_allow([^m4_getopt_h$])])dnl
m4_getopt_h=src/getopt.h
rm -f $m4_getopt_h
AC_CHECK_FUNC(getopt_long, [], 
[M4OBJS="getopt1.$ac_objext getopt.$ac_objext"
echo linking src/gnu-getopt.h to $m4_getopt_h
$LN_S ./gnu-getopt.h $m4_getopt_h])
AC_SUBST(M4OBJS)

M4_AC_FUNC_OBSTACK

jm_PREREQ_ERROR

AC_MSG_CHECKING(ecvt declaration)
AC_EGREP_HEADER(ecvt, stdlib.h,
  [AC_MSG_RESULT(yes); AC_DEFINE(HAVE_EFGCVT, 2,
[Define to 1 if you have ecvt(3), fcvt(3) and gcvt(3), define to 2 if
   these are declared in <stdlib.h>.])],
  [AC_MSG_RESULT(no); AC_CHECK_FUNCS(ecvt)])

M4_AC_SYS_STACKOVF

AC_MSG_CHECKING(if changeword is wanted)
AC_ARG_ENABLE(changeword,
[  --enable-changeword     enable -W and changeword() builtin],
[if test "$enableval" = yes; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(ENABLE_CHANGEWORD, 1,
  [Define to 1 if the changeword(REGEXP) functionality is required.])
else
  AC_MSG_RESULT(no)
fi], [AC_MSG_RESULT(no)])

AC_MSG_CHECKING(for modules to preload)
ifdef([m4_pattern_allow], [m4_pattern_allow([^m4_default_preload$])])dnl
m4_default_preload="m4 traditional gnu"
AC_ARG_WITH(modules,
[  --with-modules=MODULES preload MODULES [default: $m4_default_preload]],
[use_modules="$withval"], [use_modules="$m4_default_preload"])
test -z "$use_modules" && use_modules=no
DLPREOPEN="-dlpreopen force"
if test "$use_modules" = no; then
  AC_MSG_RESULT(none)
else
  AC_MSG_RESULT($use_modules)
  if test "$use_modules" != yes; then
    DLPREOPEN=
    for module in $use_modules; do
      DLPREOPEN="$DLPREOPEN -dlpreopen ../modules/$module.la"
    done
  fi
fi
AC_SUBST(DLPREOPEN)

dnl This is for the modules
AC_STRUCT_TM
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(getcwd gethostname mktime uname)
AC_CHECK_FUNCS(setenv unsetenv putenv clearenv)


AM_WITH_GMP

AM_WITH_DMALLOC

if test "$USE_INCLUDED_LIBINTL" = yes; then
  INTLINCL='-I$(top_srcdir)/intl'

  AC_SUBST(INTLINCL)
fi


dnl AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

Xsed="sed -e s/^X//"
LTLIBOBJS=`echo X"$LIBOBJS" | \
[$Xsed -e 's,\.[^.]* ,.lo ,g;s,\.[^.]*$,.lo,']`
AC_SUBST(LTLIBOBJS)

AC_SUBST(ac_aux_dir)

# Big hack below: 2.13 does not guarantee the value of $srcdir, so
# do not use `sh $srcdir/tests/mkconfig.sh' since if we build
# in a separate dir, it points to srcdir for... `examples/'!
ifdef([m4_pattern_allow], [m4_pattern_allow([^m4_top_srcdir$])])dnl
AC_OUTPUT(
Makefile
config/Makefile
intl/Makefile
po/Makefile.in
doc/Makefile
m4/Makefile
src/Makefile
modules/Makefile
tests/Makefile
examples/Makefile,
[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile; \
sh $m4_top_srcdir/tests/mkconfig.sh; \
test -z "$CONFIG_HEADERS" || echo timestamp > stamp-h.in],
[m4_top_srcdir=$srcdir])

